use ...Bool
use ..Equals
use ..Raw

public trait Compare {
  fun compareTo(other: This): Less | Equal | Greater
}
impl Compare: InfixLess[This, Bool] {
  fun infixLess(other: This): Bool {
    this.compareTo(other).match[Bool](less = { true() }, equal = { false() }, greater = { false() })
  }
}
impl Compare: InfixLessEqual[This, Bool] {
  fun infixLessEqual(other: This): Bool {
    this.compareTo(other).match[Bool](less = { true() }, equal = { true() }, greater = { false() })
  }
}
impl Compare: Equals {
  fun equals(other: This): Bool {
    this.compareTo(other).match[Bool](less = { false() }, equal = { true() }, greater = { false() })
  }
}
impl Compare: InfixGreaterEqual[This, Bool] {
  fun infixGreaterEqual(other: This): Bool {
    this.compareTo(other).match[Bool](less = { false() }, equal = { true() }, greater = { true() })
  }
}
impl Compare: InfixGreater[This, Bool] {
  fun infixGreater(other: This): Bool {
    this.compareTo(other).match[Bool](less = { false() }, equal = { false() }, greater = { true() })
  }
}

## TODO(later, marcelgarus): Maybe put these methods on `Iterate`?
public fun min[T: Compare](first: T, second: T): T {
  if[T](first <= second, { first }).else({ second })
}
public fun max[T: Compare](first: T, second: T): T {
  if[T](first >= second, { first }).else({ second })
}
