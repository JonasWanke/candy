use ..Collections
use ..Int

public trait Hash {
  # A type that can be hashed.

  fun hash[H: Hasher](): H
  # Uses `H`'s static `primitive` and `combine` functions to hash primitive bytes into `H`s and
  # combine multiple `H`s into one single `H`.
  #
  # The `H` can be used to customize how good and fast the hash is. For example, for simple
  # microcontroller programms, you might want to hash into a `UInt16` efficiently, but for a big
  # book library server, you might want a strong hash with a few hundred bits of entropy.

  fun hashCode(): Int { this.hash[IntHasher]().finish() }
  # Hashes `this` into an `Int` using the default `IntHasher`.
}

public trait Hasher {
  # A trait for hashing arbitrary bytes.

  static fun primitive(value: Int): This
  # Creates a new hash value based on a `Int`.
  ## TODO(marcelgarus): Make this accept a UInt8.

  static fun combine(values: Iterate[This]): This
  # Combines multiple values of this type into a single one.
}

type IntHasher = Int
impl IntHasher: Hasher {
  static fun primitive(value: Int): This { IntHasher(value) }
  static fun combine(values: Iterate[This]): This { values.map({ it.value }).sum() }
  fun finish(): Int { this.value }
}
