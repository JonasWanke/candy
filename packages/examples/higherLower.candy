# Repeatedly asks you for a guess and tells you whether the secret number is
# higher or lower. Exits once you found the secret number.

[async, await, channel, equals, ifElse, int, loop, result, run, text] = use "Core"

main := { environment ->
  print message =
    needs (text.is message)
    environment.stdout | channel.send message

  read = {
    response = channel.create 1
    environment.stdin | channel.send response.sendPort
    await response.receivePort
  }

  print "Welcome to the number guessing game!"

  correct = 42
  loop {
    print "What's your guess?"
    guess = run read | text.trim | int.parse

    guess | result.mapOrElse
      { guess ->
        int.compareTo guess correct %
          Equal ->
            print "You did it!"
            Break
          Less ->
            print "Higher!"
            Continue
          Greater ->
            print "Lower!"
            Continue
      }
      { _ ->
        print "Your guess must be a number."
        Continue
      }
  }
}
