use ast
use compiler_utils
use incremental
use ...declarations

// These impls make declarative information from the AST available in the HIR.

trait MightNotExist {
  fun doesExist(context: QueryContext<List<CompilerError>>): Bool
}
trait HasAst<Ast> {
  fun ast(context: QueryContext<List<CompilerError>>): Ast
}

fun fileToHirModule(context: QueryContext<List<CompilerError>>, file: FancyFile): HirModule {
  // TODO(marcelgarus): Implement this as a static function on `HirModule` (`HirModule fromFile`) as
  // soon as adding static functions through impls works.

  query<HirModule, List<CompilerError>>(context, "HirModule.fromFile", (file as Equals & Hash), {
    mut let path = file.path
    if path.baseNameWithoutExtension() == "module" {
      path = path.parent().unwrap()
    }
    let module = ((if path.parent() is None {
      HirTopLevelModule(file.package)
    } else {
      fileToHirModule(context, FancyFile(file.package, path.parent().unwrap()))
        .innerModule(path.baseNameWithoutExtension())
    }) as HirModule)
    Tuple(module, List.empty<CompilerError>())
  })
}

trait HasPackageAndPath {
  fun package(): Package
  fun path(): List<String>
}
impl HirModule: HasPackageAndPath {
  fun package(): Package
  fun path(): List<String>
}
impl HirTopLevelModule: HasPackageAndPath {
  fun package(): Package { parent }
  fun path(): List<String> { List.empty<String>() }
}
impl HirInnerModule: HasPackageAndPath {
  fun package(): Package { parent.package() }
  fun path(): List<String> {
    (parent.path() as Iterable<String>).followedBy(List.of1<String>(name())).toList()
  }
}

public trait HasDeclarations {
  fun declarations(context: QueryContext<List<CompilerError>>): List<HirDeclaration>
  /// Returns the child declarations of this declaration.
  ///
  /// Note that some declarations can never have any child declarations (for example, functions or
  /// properties). These always return empty lists.

  fun typeUnsafeAstDeclarations(context: QueryContext<List<CompilerError>>): List<Any>
  /// Getter for ast declarations, but type unsafe. Still useful if it's immediately followed by
  /// `whereType()`.
}
impl HirDeclaration: HasDeclarations

// TODO(marcelgarus): `HirModule`s implement the `HasDeclarations` trait. Because the Dart lowering
// is weird, we can't just add impls to traits (they will be mixed in, but the subclasses only
// implement the trait, losing the specific implementation).
// So, we put the code of the impl into the following few functions, which are used by the concrete
// classes `HirTopLevelModule` and `HirInnerModule`, passing themselves as parameters.

fun astOfModule(context: QueryContext<List<CompilerError>>, moduleHir: HirModule): Maybe<AstFile | AstModule> {
  query<Maybe<AstFile | AstModule>, List<CompilerError>>(context, "HirModule.ast", (moduleHir as Equals & Hash), {
    // These are all the ways that modules can be declared with decreasing priority:
    //
    // 1. By creating directories.
    // 2. By creating files.
    // 3. By creating inline modules.
    //
    // First, go into directories with correct names as long as possible. Then, look for a file with
    // the name of the next submodule, defaulting to module.candy if it doesn't exist. Finally, read
    // that file and find a inline module nested.

    let package = moduleHir.package()
    mut let segments = moduleHir.path() as Iterable<String>
    mut let path = Path.parse("src")

    // Follow the directory structure down as much as possible.
    while segments.isNotEmpty() {
      // TODO(marcelgarus): Move this into the while condition once the && is lazy.
      let newPath = path.child(segments.first().unwrap())
      let resolvedPath = FancyFile(package, newPath).getResolvedPath(context)
      if !Directory(resolvedPath).doesExist() { break unit }

      path = newPath
      segments = segments.skip(1)
    }

    // Choose the correct file, default to "module.candy".
    //
    // TODO(marcelgarus): Inline the condition into the `if` once the && is lazy.
    mut let condition = segments.isNotEmpty()
    if condition {
      condition = FancyFile(package, path.child("{segments.first().unwrap()}.candy"))
        .doesExist(context)
    }
    if condition {
      path = path.child("{segments.first().unwrap()}.candy")
      segments = segments.skip(1)
    } else {
      path = path.child("module.candy")
    }
    if !FancyFile(package, path).doesExist(context) {
      return Tuple(None<AstFile | AstModule>(), List.empty<CompilerError>())
    }

    // TODO(marcelgarus): Navigate through the AST to find the inline `AstModule`.
    let ast = parseAstOfFile(context, FancyFile(package, path)).first
    if segments.isNotEmpty() {
      todo("Support getting the AST of inline modules.")
    }

    return Tuple(Some<AstFile | AstModule>(ast), List.empty<CompilerError>())
  })
}
fun astDeclarationsOfModule(
  context: QueryContext<List<CompilerError>>,
  module: HirModule,
): List<AstModule | AstTrait | AstImpl | AstClass | AstFunction | AstProperty> {
  query<
    List<AstModule | AstTrait | AstImpl | AstClass | AstFunction | AstProperty>,
    List<CompilerError>,
  >(context, "HirModule.astDeclarations", (module as Equals & Hash), {
    let ast = astOfModule(context, module)
    if ast is None<AstFile | AstModule> {
      return Tuple(
        List.empty<AstModule | AstTrait | AstImpl | AstClass | AstFunction | AstProperty>(),
        List.empty<CompilerError>(),
      )
    }
    let ast = ast.unwrap()
    if (ast is AstFile) {
      return Tuple((ast as AstFile).declarations, List.empty<CompilerError>())
    }
    if (ast is AstModule) {
      return Tuple((ast as AstModule).innerDeclarations, List.empty<CompilerError>())
    }
    throw "Shouldn't reach this."
  })
}
fun declarationsOfModule(
  context: QueryContext<List<CompilerError>>,
  module: HirModule,
): List<HirDeclaration> {
  query<List<HirDeclaration>, List<CompilerError>>(
    context,
    "HirModule.declarations",
    module as Equals & Hash,
    {
      mut let implDisambiguator = 0
      mut let functionDisambiguator = 0
      let declarations = (astDeclarationsOfModule(context, module) as Iterable<AstModule | AstTrait | AstImpl | AstClass | AstFunction | AstProperty>)
        .map<HirDeclaration>({
          if (it is AstModule) { return HirInnerModule(module, (it as AstModule).name.value) as HirDeclaration }
          if (it is AstTrait) { return HirTrait(module, (it as AstTrait).name.value) as HirDeclaration }
          if (it is AstImpl) {
            let impl_ = HirImpl(module, implDisambiguator)
            implDisambiguator = implDisambiguator + 1
            return impl_ as HirDeclaration
          }
          if (it is AstClass) { return HirClass(module, (it as AstClass).name.value) as HirDeclaration }
          if (it is AstFunction) {
            let function = HirFunction(module, (it as AstFunction).name.value, functionDisambiguator)
            functionDisambiguator = functionDisambiguator + 1
            return function as HirDeclaration
          }
          if (it is AstProperty) { return HirProperty(module, (it as AstProperty).name.value) as HirDeclaration }
          throw "Shouldn't reach this."
        })
        .toList()
      Tuple(declarations, List.empty<CompilerError>())
    }
  )
}
impl HirModule: MightNotExist & HasAst<AstFile | AstModule> & HasDeclarations {
  fun doesExist(context: QueryContext<List<CompilerError>>): Bool
  fun ast(context: QueryContext<List<CompilerError>>): AstFile | AstModule
  fun astDeclarations(
    context: QueryContext<List<CompilerError>>,
  ): List<AstModule | AstTrait | AstImpl | AstClass | AstFunction | AstProperty>
}
impl HirTopLevelModule: MightNotExist & HasAst<AstFile | AstModule> & HasDeclarations {
  fun doesExist(context: QueryContext<List<CompilerError>>): Bool {
    astOfModule(context, this) is Some<AstFile | AstModule>
  }
  fun ast(context: QueryContext<List<CompilerError>>): AstFile | AstModule {
    astOfModule(context, this).unwrap()
  }
  fun astDeclarations(
    context: QueryContext<List<CompilerError>>,
  ): List<AstModule | AstTrait | AstImpl | AstClass | AstFunction | AstProperty> {
    astDeclarationsOfModule(context, this)
  }
  fun typeUnsafeAstDeclarations(context: QueryContext<List<CompilerError>>): List<Any> {
    astDeclarations(context) as List<Any>
  }
  public fun declarations(context: QueryContext<List<CompilerError>>): List<HirDeclaration> {
    declarationsOfModule(context, this)
  }
}
impl HirInnerModule: MightNotExist & HasAst<AstFile | AstModule> & HasDeclarations {
  fun doesExist(context: QueryContext<List<CompilerError>>): Bool {
    astOfModule(context, this) is Some<AstFile | AstModule>
  }
  fun ast(context: QueryContext<List<CompilerError>>): AstFile | AstModule {
    astOfModule(context, this).unwrap()
  }
  fun astDeclarations(
    context: QueryContext<List<CompilerError>>,
  ): List<AstModule | AstTrait | AstImpl | AstClass | AstFunction | AstProperty> {
    astDeclarationsOfModule(context, this)
  }
  fun typeUnsafeAstDeclarations(context: QueryContext<List<CompilerError>>): List<Any> {
    astDeclarations(context) as List<Any>
  }
  public fun declarations(context: QueryContext<List<CompilerError>>): List<HirDeclaration> {
    declarationsOfModule(context, this)
  }
}

impl HirTrait: HasAst<AstTrait> & HasDeclarations {
  fun ast(context: QueryContext<List<CompilerError>>): AstTrait {
    query<AstTrait, List<CompilerError>>(context, "HirTrait.ast", this, {
      let ast = ((parent as HasDeclarations).typeUnsafeAstDeclarations(context) as Iterable<Any>)
        .whereType<AstTrait>()
        .where({ it.name.value == name_ })
        .single()
        .unwrap()
      Tuple(ast, List.empty<CompilerError>())
    })
  }

  fun astDeclarations(
    context: QueryContext<List<CompilerError>>
  ): List<AstTrait | AstClass | AstFunction | AstProperty> {
    query<List<AstTrait | AstClass | AstFunction | AstProperty>, List<CompilerError>>(
      context,
      "HirTrait.astDeclarations",
      this,
      { Tuple(ast(context).innerDeclarations, List.empty<CompilerError>()) },
    )
  }
  fun typeUnsafeAstDeclarations(context: QueryContext<List<CompilerError>>): List<Any> {
    astDeclarations(context) as List<Any>
  }

  public fun declarations(
    context: QueryContext<List<CompilerError>>,
  ): List<HirDeclaration> {
    query<List<HirDeclaration>, List<CompilerError>>(
      context,
      "HirTrait.declarations",
      this,
      {
      mut let functionDisambiguator = 0
      let declarations = (astDeclarations(context) as Iterable<AstTrait | AstClass | AstFunction | AstProperty>)
        .map<HirDeclaration>({
          if (it is AstTrait) { return (HirTrait(this, (it as AstTrait).name.value) as HirDeclaration) }
          if (it is AstClass) { return (HirClass(this, (it as AstClass).name.value) as HirDeclaration) }
          if (it is AstFunction) {
            let function = HirFunction(this, (it as AstFunction).name.value, functionDisambiguator)
            functionDisambiguator = functionDisambiguator + 1
            return (function as HirDeclaration)
          }
          if (it is AstProperty) {
            return (HirProperty(this, (it as AstProperty).name.value) as HirDeclaration)
          }
          throw "Shouldn't reach this."
        })
        .toList()
      Tuple(declarations, List.empty<CompilerError>())
      }
    )
  }
}

impl HirImpl: HasAst<AstImpl> & HasDeclarations {
  fun ast(context: QueryContext<List<CompilerError>>): AstImpl {
    query<AstImpl, List<CompilerError>>(context, "HirImpl.ast", this, {
      let ast = ((parent as HasDeclarations).typeUnsafeAstDeclarations(context) as Iterable<Any>)
        .whereType<AstImpl>()
        .get(disambiguator)
        .unwrap()
      Tuple(ast, List.empty<CompilerError>())
    })
  }

  fun astDeclarations(context: QueryContext<List<CompilerError>>): List<AstFunction | AstProperty> {
    query<List<AstFunction | AstProperty>, List<CompilerError>>(context, "HirImpl.astDeclarations", this, {
      Tuple(ast(context).innerDeclarations, List.empty<CompilerError>())
    })
  }
  fun typeUnsafeAstDeclarations(context: QueryContext<List<CompilerError>>): List<Any> {
    astDeclarations(context) as List<Any>
  }

  public fun declarations(context: QueryContext<List<CompilerError>>): List<HirDeclaration> {
    query<List<HirDeclaration>, List<CompilerError>>(context, "HirImpl.declarations", this, {
      mut let functionDisambiguator = 0
      let declarations = (ast(context).innerDeclarations as Iterable<AstFunction | AstProperty>)
        .map<HirDeclaration>({
          if (it is AstFunction) {
            let function = HirFunction(this, (it as AstFunction).name.value, functionDisambiguator)
            functionDisambiguator = functionDisambiguator + 1
            return (function as HirDeclaration)
          }
          if (it is AstProperty) {
            return (HirProperty(this, (it as AstProperty).name.value) as HirDeclaration)
          }
          throw "Shouldn't reach this."
        })
        .toList()
      Tuple(declarations, List.empty<CompilerError>())
    })
  }
}

impl HirClass: HasAst<AstClass> & HasDeclarations {
  fun ast(context: QueryContext<List<CompilerError>>): AstClass {
    query<AstClass, List<CompilerError>>(context, "HirClass.ast", this, {
      let ast = ((parent as HasDeclarations).typeUnsafeAstDeclarations(context) as Iterable<Any>)
        .whereType<AstClass>()
        .where({ it.name.value == name_ })
        .single()
        .unwrap()
      Tuple(ast, List.empty<CompilerError>())
    })
  }

  fun astDeclarations(
    context: QueryContext<List<CompilerError>>
  ): List<AstTrait | AstClass | AstFunction | AstProperty> {
    query<List<AstTrait | AstClass | AstFunction | AstProperty>, List<CompilerError>>(
      context,
      "HirClass.astDeclarations",
      this,
      { Tuple(ast(context).innerDeclarations, List.empty<CompilerError>()) },
    )
  }
  fun typeUnsafeAstDeclarations(context: QueryContext<List<CompilerError>>): List<Any> {
    astDeclarations(context) as List<Any>
  }

  public fun declarations(context: QueryContext<List<CompilerError>>): List<HirDeclaration> {
    query<List<HirDeclaration>, List<CompilerError>>(
      context,
      "HirClass.declarations",
      this,
      {
        mut let functionDisambiguator = 0
        let declarations = (ast(context).innerDeclarations as Iterable<AstTrait | AstClass | AstFunction | AstProperty>)
          .map<HirDeclaration>({
            if (it is AstTrait) { return HirTrait(this, (it as AstTrait).name.value) }
            if (it is AstClass) { return HirClass(this, (it as AstClass).name.value) }
            if (it is AstFunction) {
              let function = HirFunction(this, (it as AstFunction).name.value, functionDisambiguator)
              functionDisambiguator = functionDisambiguator + 1
              return function
            }
            if it is AstProperty { return HirProperty(this, (it as AstProperty).name.value) }
            throw "Shouldn't reach this."
          })
          .toList()
        Tuple(declarations, List.empty<CompilerError>())
      }
    )
  }
}

impl HirFunction: HasAst<AstFunction> & HasDeclarations {
  fun ast(context: QueryContext<List<CompilerError>>): AstFunction {
    query<AstFunction, List<CompilerError>>(context, "HirFunction.ast", this, {
      let ast = ((parent as HasDeclarations).declarations(context) as Iterable<Any>)
        .whereType<AstFunction>()
        .get(disambiguator)
        .unwrap()
      Tuple(ast, List.empty<CompilerError>())
    })
  }
  
  fun typeUnsafeAstDeclarations(context: QueryContext<List<CompilerError>>): List<Any> {
    List.empty<Any>()
  }
  public fun declarations(context: QueryContext<List<CompilerError>>): List<HirDeclaration> {
    List.empty<HirDeclaration>()
  }
}

impl HirProperty: HasAst<AstProperty> & HasDeclarations {
  fun ast(context: QueryContext<List<CompilerError>>): AstProperty {
    query<AstProperty, List<CompilerError>>(context, "HirProperty.ast", this, {
      let ast = ((parent as HasDeclarations).declarations(context) as Iterable<Any>)
        .whereType<AstProperty>()
        .where({ it.name.value == name() })
        .single()
        .unwrap()
      Tuple(ast, List.empty<CompilerError>())
    })
  }

  fun isMutable(context: QueryContext<List<CompilerError>>): Bool { ast(context).isMutable }

  fun typeUnsafeAstDeclarations(context: QueryContext<List<CompilerError>>): List<Any> {
    List.empty<Any>()
  }
  public fun declarations(context: QueryContext<List<CompilerError>>): List<HirDeclaration> {
    List.empty<HirDeclaration>()
  }
}

impl HirConstructor: HasDeclarations {
  fun typeUnsafeAstDeclarations(context: QueryContext<List<CompilerError>>): List<Any> {
    List.empty<Any>()
  }
  public fun declarations(context: QueryContext<List<CompilerError>>): List<HirDeclaration> {
    List.empty<HirDeclaration>()
  }
}
