use ast
use compiler_utils
use incremental

use ...declarations
use ...expressions
use ...types
use ...solver
use ..declarations
use ..resolving

/// These impls make type system information available.

impl HirReflectionType: HasDeclarations {
  fun typeUnsafeAstDeclarations(context: QueryContext<List<CompilerError>>): List<Any> {
    List.empty<Any>()
  }
  public fun declarations(context: QueryContext<List<CompilerError>>): List<HirDeclaration> {
    List.empty<HirDeclaration>()
  }
}

// TODO(never, marcelgarus): Convert this into extension methods on Ast as soon as impls for types from
// other packages work.
public fun astInlineTypeToHirInlineType(
  context: QueryContext<List<CompilerError>>,
  astType: AstInlineType,
  scope: HirDeclaration,
): HirInlineType {
  query<HirInlineType, List<CompilerError>>(
    context,
    "astInlineTypeToHirInlineType",
    DataTuple2<AstInlineType, HirDeclaration>(astType, scope) as Equals & Hash,
    {
      if (astType is AstNamedType) {
        let nameParts = ((astType as AstNamedType).nameParts as Iterable<AstIdentifier>)
          .map<String>({ it.value })

        if ((nameParts as Iterable).length() == 1) {
          if (nameParts as Iterable<String>).first().unwrap() == "This" {
            if (scope is HirTrait || scope is HirImpl) {
              return Tuple(
                HirThisType(scope as HirTrait | HirImpl) as HirInlineType,
                List.empty<CompilerError>(),
              )
            } else {
              return Tuple(
                HirErrorType() as HirInlineType,
                List.of1<CompilerError>(
                  ThisTypeCanOnlyBeUsedInImplOrTraitCompilerError((astType as AstNamedType), scope),
                ),
              )
            }
          }
        }

        // Try to find the first part of the `nameParts`. Then, find children inside its
        // declarations.
        // TODO(never, marcelgarus): When resolving `Foo Bar`, we currently accept a `Bar` next to `Foo` if
        // `Foo` itself doesn't contain a `Bar`, as the resolver traverses parent scopes.
        mut let candidates = resolveIdentifier(context, nameParts.first().unwrap(), scope)
        for namePart in nameParts.skip(1) {
          candidates = (candidates as Iterable<HirDeclaration>)
            .expand<HirModule | HirType | HirTrait | HirFunction | HirParameterType>({
              if (it is HirDeclaration) {
                resolveIdentifier(context, namePart, it).items()
              } else {
                List.empty<HirModule | HirType | HirTrait | HirFunction | HirParameterType>()
              }
            })
            .toSet()
        }
        let candidates = candidates.items()
          .where({ !(it is HirModule) })
          // This cast from `HirType | HirTrait | HirFunction | HirParameterType` to
          // `HirType | HirTrait | HirParameterType` succeeds because the name is uppercase, so no
          // functions get resolved.
          .cast<HirType | HirTrait | HirParameterType>()
          .toSet()

        // There should be exactly one candidate left.
        if (candidates.items() as Iterable).isEmpty() {
          return Tuple(
            HirErrorType() as HirInlineType,
            List.of1<CompilerError>(UnknownTypeCompilerError(astType as AstNamedType)),
          )
        }
        if (candidates.items() as Iterable).length() > 1 {
          return Tuple(
            HirErrorType() as HirInlineType,
            List.of1<CompilerError>(
              AmbiguousTypeCompilerError(
                candidates as Set<HirModule | HirType | HirTrait | HirParameterType>,
              ),
            ),
          )
        }
        let candidate = (candidates.items() as Iterable<HirType | HirTrait | HirParameterType>).single().unwrap()
        if (candidate is HirParameterType) {
          return Tuple(candidate as HirParameterType, List.empty<CompilerError>())
        }
        let declaration = (candidate as HirTrait | HirType)

        let types = ((astType as AstNamedType).typeArguments as Iterable<AstTypeArgument>)
          .map<HirInlineType>({ astInlineTypeToHirInlineType(context, it.type, scope) })
          .toList()
        return Tuple(HirNamedType(declaration, types) as HirInlineType, List.empty<CompilerError>())
      }

      if (astType is AstFunctionType) {
        return Tuple(
          HirFunctionType(
            (astType as AstFunctionType).receiverType
              .map<HirInlineType>({ astInlineTypeToHirInlineType(context, it, scope) }),
            ((astType as AstFunctionType).parameterTypes as Iterable<AstInlineType>)
              .map<HirInlineType>({ astInlineTypeToHirInlineType(context, it, scope) })
              .toList(),
            astInlineTypeToHirInlineType(context, (astType as AstFunctionType).returnType, scope),
          ) as HirInlineType,
          List.empty<CompilerError>()
        )
      }

      if (astType is AstPositionalTupleType) {
        return Tuple(
          HirNamedTupleType(
            ((astType as AstPositionalTupleType).types as Iterable<AstInlineType>)
              .mapIndexed<(String, HirInlineType)>({ type, index =>
                Tuple("${index}", astInlineTypeToHirInlineType(context, type, scope))
              })
              .toList(),
          ) as HirInlineType,
          List.empty<CompilerError>(),
        )
      }

      if (astType is AstNamedTupleType) {
        return Tuple(
          HirNamedTupleType(
            ((astType as AstNamedTupleType).types as Iterable<AstNamedTupleTypeField>)
              .map<(String, HirInlineType)>({
                Tuple(it.name.value, astInlineTypeToHirInlineType(context, it.type, scope))
              })
              .toList(),
          ) as HirInlineType,
          List.empty<CompilerError>(),
        )
      }

      if (astType is AstEnumType) {
        let loweredVariants = ((astType as AstEnumType).variants as Iterable<AstEnumTypeVariant>)
          .map<(String, Maybe<HirInlineType>)>({
            let valueType = it.valueType.map<HirInlineType>({ valueType =>
              astInlineTypeToHirInlineType(context, valueType, scope)
            })
            Tuple(it.name.value, valueType)
          })
          .toList()
        return Tuple(
          HirEnumType(
            (loweredVariants as Iterable<(String, Maybe<HirInlineType>)>)
              .unsafeToMap<String, Maybe<HirInlineType>>(),
          ) as HirInlineType,
          List.empty<CompilerError>(),
        )
      }

      if (astType is AstIntersectionType) {
        return Tuple(
          HirIntersectionType(
            ((astType as AstIntersectionType).types as Iterable<AstInlineType>)
              .map<HirInlineType>({ astInlineTypeToHirInlineType(context, it, scope) })
              .toList(),
          ) as HirInlineType,
          List.empty<CompilerError>(),
        )
      }

      if (astType is AstErrorType) {
        return Tuple(HirErrorType(), List.empty<CompilerError>())
      }

      throw "Unknown AST type: {astType}"
    }
  )
}

impl HirParameterType {
  fun upperBound(context: QueryContext<List<CompilerError>>): HirInlineType {
    query<HirInlineType, List<CompilerError>>(context, "HirParameterType.upperBound", this, {
      let astTypeParameter = (((this.declaration as HasAst).ast(context) as AstWithTypeParameters)
        .typeParameters as Iterable<AstTypeParameter>)
        .singleWhere({ it.name.value == this.name })
        .unwrap()
      Tuple(
        astTypeParameter.upperBound
          .map<HirInlineType>({ astInlineTypeToHirInlineType(context, it, this.declaration) })
          .orElse({ HirNamedType.coreAny(context) }),
        List.empty<CompilerError>(),
      )
    })
  }
}

impl HirTrait {
  fun upperBound(context: QueryContext<List<CompilerError>>): HirInlineType {
    query<HirInlineType, List<CompilerError>>(context, "HirTrait.upperBound", this, {
      Tuple(
        this.ast(context).upperBound
          .map<HirInlineType>({ astInlineTypeToHirInlineType(context, it, this) })
          .orElse({ HirNamedType.coreAny(context) }),
        List.empty<CompilerError>(),
      )
    })
  }
}

impl HirImpl {
  fun constraints(context: QueryContext<List<CompilerError>>): Map<HirParameterType, HirInlineType> {
    // The type constraints of an impl. For example, `impl[T: Equals] Foo[T]: Equals` has the
    // constraints `T: Equals`.

    query<Map<HirParameterType, HirInlineType>, List<CompilerError>>(
      context, "HirImpl.constraints", this, {
      Tuple(
        (this.typeParameters(context) as Iterable<HirParameterType>)
          .map<(HirParameterType, HirInlineType)>({ Tuple(it, it.upperBound(context)) })
          .unsafeToMap<HirParameterType, HirInlineType>(),
        List.empty<CompilerError>(),
      )
    })
  }

  fun baseType(context: QueryContext<List<CompilerError>>): HirNamedType {
    // The base type of an impl. For example, `impl Foo[Int]` has the base type `Foo[Int]`.

    query<HirNamedType, List<CompilerError>>(context, "HirImpl.baseType", this as Equals & Hash, {
      Tuple(
        astInlineTypeToHirInlineType(context, this.ast(context).type, this) as HirNamedType,
        List.empty<CompilerError>(),
      )
    })
  }

  fun implementedTrait(context: QueryContext<List<CompilerError>>): Maybe<HirInlineType> {
    // The implemented trait of this impl. For example, `impl Foo: Equals` implements trait
    // `Equals`, whereas `impl Foo { ... }` implements none.

    query<Maybe<HirInlineType>, List<CompilerError>>(context, "HirImpl.implementedTrait", this as Equals & Hash, {
      let traits = this.ast(context).traits
      if !((traits as Iterable).length() == 1) {
        // TODO(never, marcelgarus): Support impls for more than one trait.
        return Tuple(
          None<HirInlineType>(),
          List.empty<CompilerError>(
            // UnsupportedFeatureCompilerError(
            //   Some<Location>(Location(, Span(0, 1))),
            //   "This impl is for more than one trait: {this}",
            // ),
          ),
        )
      }
      Tuple(
        Some<HirInlineType>(
          astInlineTypeToHirInlineType(
            context,
            (traits as Iterable).cast<AstInlineType>().single().unwrap(),
            // TODO(never, marcelgarus): Directly cast this into a `Iterable<AstInlineType>` when the Dart
            // type system gets good enough.
            this,
          ),
        ),
        List.empty<CompilerError>(),
      )
    })
  }
}

impl HirFunction {
  fun returnType(context: QueryContext<List<CompilerError>>): HirInlineType {
    query<HirInlineType, List<CompilerError>>(context, "HirFunction.returnType", this, {
      Tuple(
        this.ast(context).returnType
          .map<HirInlineType>({ astInlineTypeToHirInlineType(context, it, this) })
          .orElse({ HirNamedType.coreNothing(context) }),
        List.empty<CompilerError>(),
      )
    })
  }
}

fun getAllImplsInScope(context: QueryContext<List<CompilerError>>, scope: Package): Set<HirImpl> {
  query<Set<HirImpl>, List<CompilerError>>(context, "getAllImplsInScope", scope as Equals & Hash, {
    let impls = (scope.getAllPackagesInCompilationOrder(context) as Iterable<Package>)
      .map<Set<HirImpl>>({ package => getAllImplsOfModule(context, HirTopLevelModule(package)) })
      .foldLeft<Set<HirImpl>>(Set.empty<HirImpl>(), { a, b => a.union(b) })
    Tuple(impls, List.empty<CompilerError>())
  })
}
fun getAllImplsOfModule(context: QueryContext<List<CompilerError>>, module: HirModule): Set<HirImpl> {
  query<Set<HirImpl>, List<CompilerError>>(context, "getAllImplsOfModule", module as Equals & Hash, {

    let declarations = (declarationsOfModule(context, module) as Iterable<HirDeclaration>)
    let impls = declarations
      .whereType<HirModule>()
      .expand<HirImpl>({ (getAllImplsOfModule(context, it) as Iterable<HirImpl>) })
      .followedBy(declarations.whereType<HirImpl>())
      .toSet()
    Tuple(impls, List.empty<CompilerError>())
  })
}

fun hirImplToSolverRules(
  context: QueryContext<List<CompilerError>>,
  hirImpl: HirImpl,
): Set<SolverRule> {
  query<Set<SolverRule>, List<CompilerError>>(
    context,
    "hirImplToSolverRules",
    hirImpl as Equals & Hash, {
    // Lower the constraints. For example, in the impl `impl[T: Equals] Foo[T]: Equals`, the
    // `solverConstraints` are a list containing `Equals(?T)`.
    let rules = MutableSet.empty<SolverRule>()
    let solverConstraints = MutableList.empty<SolverGoal>()
    for constraint in hirImpl.constraints(context).entries() {
      let result = hirInlineTypeToSolverTypeAndGoalsAndRules(context, constraint.second)
      rules.addAll(result.third)
      solverConstraints.appendAll(
        (result.second as Iterable<SolverGoal>).map<SolverGoal>({
          it.substituteAll(
            Map.of1<SolverVariable, SolverType>(
              Tuple(
                result.first as SolverVariable,
                SolverVariable(Some<HirParameterType>(constraint.first)),
              ),
            ),
          )
        }),
      )
    }

    // Lower the base type. For example, in the impl `impl Iterable[Int]: Foo`, the base type
    // `Iterable[Int]` gets lowered to `?0` with the goal `Iterable(Int, ?0)`.
    let baseType = hirImpl.baseType(context)
    let result = hirInlineTypeToSolverTypeAndGoalsAndRules(context, baseType)
    rules.addAll(result.third)
    solverConstraints.appendAll(result.second)
    let solverBase = result.first

    // Lower the trait. For example, in the impl `impl Foo: Iterable[Int]`, the implemented trait
    // `Iterable[Int]` gets lowered to `?0` with the goals `Iterable(Int, ?0)`. Impls that don't
    // implement a trait – like `impl Foo { ... }` – don't correspond to a `SolverRule` and cause
    // this function to return `None`.
    let traitType = hirImpl.implementedTrait(context)
    if (traitType is None) { return Tuple(None<SolverRule>(), List.empty<CompilerError>()) }
    if (traitType.unwrap() is HirErrorType) {
      return Tuple(None<SolverRule>(), List.empty<CompilerError>())
    }
    let traitType = (traitType.unwrap() as HirNamedType)
    let result = hirInlineTypeToSolverTypeAndGoalsAndRules(context, traitType)
    let solverTrait = (result.first as SolverVariable) // Traits get lowered to `SolverVariable`s.
    let traitGoals = result.second
    rules.addAll(result.third)
    if ((traitGoals as Iterable).length() > 1) {
      // TODO(never, marcelgarus): We can't implement a trait that contains another trait yet, like
      // `Iterable<Equals>`. This does work in Rust (with boxing and explicit dynamism), so we'll
      // probably have to look at how to put general-purpose logic implications in our solver (like
      // `A B -> C D` instead of only having one implicative result like `A B -> C` in the solver).
      // Or we need to somehow reduce this to multiple simple rules.
      // For now, we're probably fine with only implementing "simple" traits like `Iterable<Int>`.
      // (As "primitive" types like `List` etc. are also traits, resolving this todo is somewhat of
      // a priority.)
      return Tuple(
        None<SolverRule>(),
        List.of1<CompilerError>(CannotImplementTraitOfTraitCompilerError(hirImpl)),
      )
    }
    assert((traitGoals as Iterable).length() == 1, "Should never happen.")
    let traitGoal = (traitGoals as Iterable<SolverGoal>).single().unwrap()

    // Let's play this through for the impl `impl[T: Equals] Iterable[T]: Equals`.
    //
    // These would be the values calculated above:
    //
    // * `constraints`: list with goal `Equals(?T)`
    // * `solverBase`: type `?0` and goal `Iterable(?T, ?0)`
    // * `solverTrait`: type `?1` and goal `Equals(?1)`
    //
    // The goal that this impl wants to achieve can be calculated by taking the `solverTrait`, which
    // is guaranteed to have only one goal `Equals(?1)`, and replacing the `solverTrait`'s type `?1`
    // with the `solverBase` type `?0` – which results in `Equals(?0)`.
    // To achieve that goal, we have to satisfy the goals of the `solverBase` and the `constraints`.
    // So, our total `SolverRule` would look like this: `Equals(?0) <- Iterable(?T, ?0), Equals(?T)`
    Tuple(
      Set.of1<SolverRule>(
        SolverRule(
          hirImpl,
          traitGoal.substituteAll(Map.of1<SolverVariable, SolverType>(Tuple(solverTrait, solverBase))),
          solverConstraints,
        ),
      ).union(rules),
      List.empty<CompilerError>(),
    )
  })
}

fun hirInlineTypeToSolverTypeAndGoalsAndRules(
  context: QueryContext<List<CompilerError>>,
  hirType: HirInlineType,
): (SolverType, List<SolverGoal>, List<SolverRule>) {
  /// Turns a `HirInlineType` into a `SolverType` and a list of `SolverGoal`s as well as a list of
  /// `SolverRule`s.
  ///
  /// Example:
  ///
  /// hirInlineTypeToSolverStuff(Tuple(String, Int)) == (
  ///   ?0,
  ///   Tuple(?0, ?1, ?2), String(?1), Int(?2),
  ///   [
  ///     Tuple($Tuple[?1, ?2], ?1, ?2) <- <empty>,
  ///     Tuple($Never, ?1, ?2) <- <empty>,
  ///     Tuple(?0, ?1, ?2) <- Error(?0),
  ///     String($String) <- <empty>,
  ///     String($Never) <- <empty>,
  ///     String(?0) <- Error(?0),
  ///     Int($Int) <- <empty>,
  ///     Int($Never) <- <empty>,
  ///     Int(?0) <- Error(?0),
  ///     Error(?0) <- <empty>,
  ///     Any(?0) <- <empty>,
  ///   ],
  /// )

  query<
    (SolverType, List<SolverGoal>, List<SolverRule>),
    List<CompilerError>,
  >(context, "hirInlineTypeToSolverTypeAndGoals", hirType, {
    if (hirType is HirErrorType) {
      return Tuple(
        Tuple(
          SolverVariable(None<HirParameterType>()),
          List.empty<SolverGoal>(),
          List.of2<SolverRule>(
            ruleThatEveryTypeImplementsError(context),
            ruleThatEveryTypeImplementsAny(context),
          ),
        ),
        List.empty<CompilerError>(),
      )
    }
    if (hirType is HirNamedType) {
      let declaration = (hirType as HirNamedType).declaration
      let parameters = ((hirType as HirNamedType).parameterTypes as Iterable<HirInlineType>)
      let rules = MutableSet.empty<SolverRule>()
      let goals = MutableList.empty<SolverGoal>()
      let solverParameters = MutableList.empty<SolverType>()
      for parameter in parameters {
        let result = hirInlineTypeToSolverTypeAndGoalsAndRules(context, parameter)
        solverParameters.append(result.first)
        goals.appendAll(result.second)
        rules.addAll(result.third)
      }
      // We'll return a substitution that will have to satisfy the trait. For example, given the
      // trait `Equals`, we'll return `?0` with the goal `Equals(?0)`. The `?0` is this
      // substitution.
      let substitution = SolverVariable(Some<HirParameterType>(canonicalVariable(-1)))
      goals.append(
        SolverGoal(
          declaration,
          (solverParameters as List<SolverType>).followedByList(List.of1<SolverType>(substitution)),
        ),
      )
      return Tuple(Tuple(substitution, goals, rules), List.empty<CompilerError>())
    }
    // TODO(never, marcelgarus): For now, we only support impls for nominal types. If we want to support
    // `impl A | B: Equals` or `impl (name: String, age: UInt): Clone`, we'll need to add support
    // for other `HirInlineTypes` as well.
    // TODO(never, marcelgarus): Handle HirFunctionType
    // TODO(never, marcelgarus): Handle HirNamedTupleType
    // TODO(never, marcelgarus): Handle HirEnumType
    // TODO(never, marcelgarus): Handle HirIntersectionType
    if (hirType is HirThisType) {
      let declaration = (hirType as HirThisType).declaration
      if (declaration is HirTrait) {
        // We don't need to lower `This` types in traits to solver types because we only use impls
        // for logical type solving.
        throw "Don't call hirTypeToSolverType for a This type in a trait"
      }
      if (declaration is HirImpl) {
        // If a `This` type is used inside an impl, it just assumes the solver type of the base
        // type.
        //
        // Whether that solver type is a `SolverVariable` or `SolverValue` depends on whether the
        // impl is for a trait or type:
        //
        // * `impl Int: InfixAmpersand[This, Int]` has the lowered base type `Int`, so the `This`
        //   gets replaced with `Int`, resulting in `InfixAmpersand(Int, Bool)`.
        // * `impl And: InfixAmpersand[This, Bool]` has the lowered base type `?0` with the
        //   additional goal `And(?0)`, so the `This` gets replaced with `?0`, resulting in
        //   `InfixAmpersand(?0, Bool) <- And(?0)`.
        let loweredBaseType = hirInlineTypeToSolverTypeAndGoalsAndRules(
          context,
          (declaration as HirImpl).baseType(context),
        )
        let baseSolverType = loweredBaseType.first
        return Tuple(
          Tuple(baseSolverType, List.empty<SolverGoal>(), loweredBaseType.third),
          List.empty<CompilerError>(),
        )
      }
    }
    if (hirType is HirParameterType) {
      return Tuple(
        Tuple(
          SolverVariable(Some<HirParameterType>(hirType as HirParameterType)),
          List.empty<SolverGoal>(),
          List.empty<SolverRule>(),
        ),
        List.empty<CompilerError>(),
      )
    }
    if (hirType is HirReflectionType) {
      todo("soon, marcelgarus: Handle HirReflectionType")
    }

    throw "Unexpected HirType: {hirType}"
  })
}

fun hirTypeOrTraitToSolverRule(
  context: QueryContext<List<CompilerError>>,
  declaration: HirType | HirTrait,
): Set<SolverRule> {
  query<Set<SolverRule>, List<CompilerError>>(
    context,
    "hirTypeOrTraitToSolverRule",
    declaration as Equals & Hash, {
      // We create several synthetic types and impls to allow for correct handling of error types
      // and the `Never` type.
      //
      // For example:
      //
      // hirTypeOrTraitToSolverRule(Int) == [
      //   Int($Int) <- <empty>,
      //   Int($Never) <- <empty>,
      //   Int(?0) <- Error(?0),
      //   Error(?0) <- <empty>,
      //   Any(?0) <- <empty>,
      // ]

      let rules = MutableSet.empty<SolverRule>()
      let goals = MutableList.empty<SolverGoal>()
      let solverParameters = MutableList.empty<SolverType>()
      for parameter in (declaration as HasTypeParameters).typeParameters(context) {
        let result = hirInlineTypeToSolverTypeAndGoalsAndRules(context, parameter.upperBound(context))
        solverParameters.append(result.first)
        goals.appendAll(result.second)
        rules.addAll(result.third)
      }
      let errorType = HirType.syntheticError(context)
      let errorVariable = SolverVariable.error()
      return Tuple(
        Set.of5<SolverRule>(
          // T($T) <- <empty>
          SolverRule(declaration, SolverGoal(declaration, solverParameters), goals),
          // T($Never) <- <empty>
          SolverRule(
            declaration,
            SolverGoal(
              declaration,
              List.of1<SolverType>(SolverValue(HirType.coreNever(context), List.empty<SolverType>())),
            ),
            List.empty<SolverGoal>(),
          ),
          // T(?0) <- Error(?0)
          SolverRule(
            declaration,
            SolverGoal(declaration, List.of1<SolverType>(errorVariable)),
            List.of1<SolverGoal>(SolverGoal(errorType, List.of1<SolverType>(errorVariable))),
          ),
          ruleThatEveryTypeImplementsError(context),
          ruleThatEveryTypeImplementsAny(context),
        ).union(rules),
        List.empty<CompilerError>(),
      )
    }
  )
}

fun ruleThatEveryTypeImplementsError(context: QueryContext<List<CompilerError>>): SolverRule {
  let errorType = HirType.syntheticError(context)
  SolverRule(
    errorType,
    SolverGoal(errorType, List.of1<SolverType>(SolverVariable.error())),
    List.empty<SolverGoal>(),
  )
}

fun ruleThatEveryTypeImplementsAny(context: QueryContext<List<CompilerError>>): SolverRule {
  let anyType = HirTrait.coreAny(context)
  SolverRule(
    anyType,
    SolverGoal(anyType, List.of1<SolverType>(SolverVariable(None<HirParameterType>()))),
    List.empty<SolverGoal>(),
  )
}


fun getSolverEnvironmentOfScope(context: QueryContext<List<CompilerError>>, scope: Package): Environment {
  query<Environment, List<CompilerError>>(
    context, "getSolverEnvironmentOfScope", scope as Equals & Hash, {
    let rules = getAllImplsInScope(context, scope).items()
      .map<Set<SolverRule>>({ hirImplToSolverRules(context, it) })
      .foldLeft<Set<SolverRule>>(Set.empty<SolverRule>(), { a, b => a.union(b) })
    Tuple(Environment(rules.items().toList()), List.empty<CompilerError>())
  })
}

fun makeSureImplsAreNotConflicting(context: QueryContext<List<CompilerError>>, scope: Package) {
  query<Unit, List<CompilerError>>(context, "makeSureImplsAreNotConflicting", scope as Equals & Hash, {
    for implsForSameTrait in (getAllImplsInScope(context, scope).items() as Iterable<HirImpl>)
      .where({ it.implementedTrait(context) is Some })
      .groupBy<HirInlineType>({ it.implementedTrait(context).unwrap() })
      .values() {
      let implsAndBasesForCurrentTrait = (implsForSameTrait as Iterable<HirImpl>)
        .map<(HirImpl, SolverType)>({
          let result = hirInlineTypeToSolverTypeAndGoalsAndRules(context, it.baseType(context))
          Tuple(it, result.first)
        })
        .toList()
      for implAndBase1 in implsAndBasesForCurrentTrait {
        for implAndBase2 in implsAndBasesForCurrentTrait {
          let impl1 = implAndBase1.first
          let impl2 = implAndBase2.first
          let base1 = implAndBase1.second
          let base2 = implAndBase2.second

          if (impl1 as Equals) == (impl2 as Equals) {
            continue
          }

          let unified = base1.unify(base2)
          if unified is Some {
            // Unifying the impl types succeeded. This means there's a type to which both impls
            // apply. That's bad.
            let conflicting = base1.substituteAll(unified.unwrap())
            return Tuple(
              unit,
              List.of1<CompilerError>(
                ConflictingImplsCompilerError(impl1, impl2, base1, base2, conflicting),
              ),
            )
          }
        }
      }
    }
    return Tuple(unit, List.empty<CompilerError>())
  })
}

public fun isAssignableTo(
  context: QueryContext<List<CompilerError>>,
  base: HirInlineType,
  trait_: HirInlineType,
  scope: Package,
): Bool {
  query<Bool, List<CompilerError>>(context, "isAssignableTo", DataTuple3<HirInlineType, HirInlineType, Package>(base, trait_, scope), {
    Tuple(implFor(context, base, trait_, scope) is Some<HirImpl>, List.empty<CompilerError>())
  })
}
public fun implFor(
  context: QueryContext<List<CompilerError>>,
  base: HirInlineType,
  trait_: HirInlineType,
  scope: Package,
): Maybe<HirImpl> {
  query<Maybe<HirImpl>, List<CompilerError>>(context, "implFor", DataTuple3<HirInlineType, HirInlineType, Package>(base, trait_, scope), {
    assert(!(trait_ is HirErrorType), "Can't reveal the impl for the error type")

    let environment = getSolverEnvironmentOfScope(context, scope)
    let baseSolverAndGoals = hirInlineTypeToSolverTypeAndGoalsAndRules(context, base)
    let baseType = baseSolverAndGoals.first
    let baseGoals = baseSolverAndGoals.second
    let traitSolverAndGoals = hirInlineTypeToSolverTypeAndGoalsAndRules(context, trait_)
    let traitType = traitSolverAndGoals.first
    let traitGoals = traitSolverAndGoals.second

    if (traitType is SolverValue) {
      throw "This shouldn't happen. Trait should be lowered to a SolverVariable."
    }
    if !((traitGoals as Iterable).length() == 1) {
      return Tuple(
        None<HirImpl>(),
        List.of1<CompilerError>(TryingToFindImplForTraitWithTraitAsParameterCompilerError(trait_)),
      )
    }

    let solution = environment.solve(
      (traitGoals as Iterable<SolverGoal>).single().unwrap().substituteAll(
        Map.of1<SolverVariable, SolverType>(Tuple((traitType as SolverVariable), baseType)),
      ),
      baseGoals,
    )
    if (solution is SolverSolutionUnique) {
      Tuple(
        Some<HirImpl>((solution as SolverSolutionUnique).usedRule.originalImpl as HirImpl),
        List.empty<CompilerError>(),
      )
    } else {
      Tuple(None<HirImpl>(), List.empty<CompilerError>())
    }
  })
}

fun getAllTraitsInScope(context: QueryContext<List<CompilerError>>, scope: Package): Set<HirTrait> {
  query<Set<HirTrait>, List<CompilerError>>(context, "getAllTraitsInScope", scope as Equals & Hash, {
    let traits = (scope.getAllPackagesInCompilationOrder(context) as Iterable<Package>)
      .map<Set<HirTrait>>({ package => getAllTraitsOfModule(context, HirTopLevelModule(package)) })
      .foldLeft<Set<HirTrait>>(Set.empty<HirTrait>(), { a, b => a.union(b) })
    Tuple(traits, List.empty<CompilerError>())
  })
}
fun getAllTraitsOfModule(context: QueryContext<List<CompilerError>>, module: HirModule): Set<HirTrait> {
  query<Set<HirTrait>, List<CompilerError>>(context, "getAllTraitsOfModule", module as Equals & Hash, {

    let declarations = (declarationsOfModule(context, module) as Iterable<HirDeclaration>)
    let traits = declarations
      .whereType<HirModule>()
      .expand<HirTrait>({ (getAllTraitsOfModule(context, it) as Iterable<HirTrait>) })
      .followedBy(declarations.whereType<HirTrait>())
      .toSet()
    Tuple(traits, List.empty<CompilerError>())
  })
}

fun getAllImplementedTraitsFor(
  context: QueryContext<List<CompilerError>>,
  base: HirInlineType,
  scope: Package,
): Set<HirNamedType> {
  query<Set<HirNamedType>, List<CompilerError>>(context, "getAllImplementedTraitsFor", DataTuple2<HirInlineType, Package>(base, scope), {
    todo("soon, marcelgarus: implement getAllImplementedTraitsFor")
    // getAllTraitsInScope(context, scope)
    //   .items()
    //   .where({ isAssignableTo(context, base, it, scope) })
    //   .toSet()
  })
}

fun getAllImplsFor(
  context: QueryContext<List<CompilerError>>,
  base: HirInlineType,
  scope: Package,
): Set<HirImpl> {
  query<Set<HirImpl>, List<CompilerError>>(context, "getAllImplsFor", DataTuple2<HirInlineType, Package>(base, scope), {
    let implementedTraits = getAllImplementedTraitsFor(context, base, scope)
    let impls = getAllImplsInScope(context, scope)
      .items()
      .where({ impl_ => implementedTraits.contains(impl_.baseType(context)) })
      .followedBy(
        getAllImplsInScope(context, scope).items().where({ base == it.baseType(context) }),
      )
      .toSet()
    Tuple(impls, List.empty<CompilerError>())
  })
}

// fun resolveFunctionOnInlineType(
//   context: QueryContext<List<CompilerError>>,
//   base: HirInlineType,
//   functionName: String,
//   scope: Package,
// ): Set<HirFunction> {
//   getAllImplsInScope()
// }

public class ThisTypeCanOnlyBeUsedInImplOrTraitCompilerError {
  public let thisType: AstNamedType
  public let scope: HirDeclaration
}
impl ThisTypeCanOnlyBeUsedInImplOrTraitCompilerError: CompilerError {
  public fun id(): String { "this-type-can-only-be-used-in-impl-or-trait" }

  public fun location(): Location {
    todo("never, marcelgarus: Implement ThisTypeCanOnlyBeUsedInImplOrTraitCompilerError.location")
  }
  public fun title(): String { "The This type can only be used in impls or traits." }
  public fun description(): String { "You can't use the This type in modules or type definitions" }
}

public class CannotImplementTraitOfTraitCompilerError {
  public let theImpl: HirImpl
}
impl CannotImplementTraitOfTraitCompilerError: CompilerError {
  public fun id(): String { "cannot-implement-trait-of-trait" }

  public fun location(): Location {
    todo("never, marcelgarus: Implement CannotImplementTraitOfTraitCompilerError.location")
  }
  public fun title(): String {
    "You cannot implement a trait containing another trait as a generic parameter yet."
  }
  public fun description(): String { "The offending impl: {theImpl}" }
}

public class TryingToFindImplForTraitWithTraitAsParameterCompilerError {
  public let trait_: HirInlineType
}
impl TryingToFindImplForTraitWithTraitAsParameterCompilerError: CompilerError {
  public fun id(): String { "trying-to-find-impl-for-trait-with-trait-as-parameter" }

  public fun location(): Location {
    todo("never, marcelgarus: Implement TryingToFindImplForTraitWithTraitAsParameterCompilerError.location")
  }
  public fun title(): String { "Conflicting impls." }
  public fun description(): String {
    "Trying to find an impl for trait {trait_}, which has another trait as a type parameter. That's not supported yet."
  }
}

public class ConflictingImplsCompilerError {
  public let impl1: HirImpl
  public let impl2: HirImpl
  public let base1: SolverType
  public let base2: SolverType
  public let conflictingType: SolverType
}
impl ConflictingImplsCompilerError: CompilerError {
  public fun id(): String { "conflicting-impls" }

  public fun location(): Location {
    todo("never, marcelgarus: Implement ConflictingImplsCompilerError.location")
  }
  public fun title(): String { "Conflicting impls." }
  public fun description(): String {
    "Impl {impl1} conflicts with {impl2}, because both bases {base1} and {base2} apply to the same type {conflictingType}."
  }
}

public class AmbiguousTypeCompilerError {
  public let candidates: Set<HirModule | HirType | HirTrait | HirParameterType>
}
impl AmbiguousTypeCompilerError: CompilerError {
  public fun id(): String { "ambiguous-type" }

  public fun location(): Location {
    todo("never, marcelgarus: Implement AmbiguousTypeCompilerError.location")
  }
  public fun title(): String { "Ambiguous type. Candidates are {candidates}." }
  public fun description(): String {
    /// TODO(never, marcelgarus): add a description for this error
    ""
  }
}

public class UnknownTypeCompilerError {
  public let astType: AstNamedType
}
impl UnknownTypeCompilerError: CompilerError {
  public fun id(): String { "could-not-resolve-type" }

  public fun location(): Location {
    todo("never, marcelgarus: Implement UnknownTypeCompilerError.location")
  }
  public fun title(): String { "Couldn't resolve type {astType.nameParts}." }
  public fun description(): String {
    /// TODO(never, marcelgarus): add a description for this error
    ""
  }
}
