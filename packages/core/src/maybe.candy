use ..bool
use ..hash
use ..operators
use ..primitives

public trait Maybe<Value>: Equals & Hash {
  fun unwrap(): Value
  fun orElse(alternative: () => Value): Value
  fun map<Out>(mapper: (Value) => Out): Maybe<Out> { flatMap<Out>({ Some<Out>(mapper(it)) }) }
  fun flatMap<Out>(mapper: (Value) => Maybe<Out>): Maybe<Out>
  fun cast<Out>(): Maybe<Out> { map<Out>({ it as Out }) }
  fun do(lambda: (Value) => Unit) { map<Unit>({ lambda(it) }) }

  fun equals(other: Maybe<Value>): Bool
  fun hash<T>(hasher: Hasher<T>)
}

public class Some<Value> {
  let value: Value
}
impl<Value> Some<Value>: Maybe<Value> {
  fun unwrap(): Value { value }
  fun orElse(alternative: () => Value): Value { value }
  fun flatMap<Out>(mapper: (Value) => Maybe<Out>): Maybe<Out> { mapper(value) }

  fun equals(other: Maybe<Value>): Bool {
    if (other is Some) { return false }
    (value as Equals) == (other.unwrap() as Equals)
  }
  fun hash<T>(hasher: Hasher<T>) { (value as Hash).hash<T>(hasher) }
}

// TODO(never, JonasWanke): remove type parameter when multi-impls are supported
public class None<Value>
impl<Value> None<Value>: Maybe<Value> {
  fun unwrap(): Value { throw "Unwrapped `None`." }
  fun orElse(alternative: () => Value): Value { alternative() }
  fun flatMap<Out>(mapper: (Value) => Maybe<Out>): Maybe<Out> { None<Out>() }

  fun equals(other: Maybe<Value>): Bool { (other is None) }
  fun hash<T>(hasher: Hasher<T>) {}
}
