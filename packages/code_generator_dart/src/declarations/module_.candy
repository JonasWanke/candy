use compiler_utils
use dart_code
use hir
use incremental

use ...compile
use ..function
use ..name_mangling
use ..trait
use ..type

public fun compileModuleAndDescendants(
  context: QueryContext<List<CompilerError>>,
  package: Package,
  moduleHir: HirModule,
): Map<Path, String> {
  query<Map<Path, String>, List<CompilerError>>(
    context,
    "code_generator_dart.compileModuleAndDescendants",
    DataTuple2<Package, HirModule>(package, moduleHir), {
    let output = MutableMap.empty<Path, String>()

    let moduleResult = compileModule(context, package, moduleHir)
    output.set(moduleResult.first, moduleResult.second)

    ((moduleHir as HasDeclarations).declarations(context) as Iterable<HirDeclaration>)
      .whereType<HirModule>()
      .do({
        output.setAll(compileModuleAndDescendants(context, package, it))
        unit
      })

    Tuple(output, List.empty<CompilerError>())
  })
}

fun compileModule(
  context: QueryContext<List<CompilerError>>,
  package: Package,
  moduleHir: HirModule,
): (Path, String) {
  query<(Path, String), List<CompilerError>>(
    context,
    "code_generator_dart.compileModule",
    DataTuple2<Package, HirModule>(package, moduleHir), {
    let outputPath = moduleHirToOutputPath(context, moduleHir)

    let declarations = MutableList.empty<DartDeclaration>()
    for declaration in (moduleHir as HasDeclarations).declarations(context) {
      if declaration is HirModule {
        // Compiled by `compileModuleAndDescendants`.
        continue
      }
      if declaration is HirTrait {
        let compiledTypes = compileTrait(context, package, declaration as HirTrait)
        declarations.appendAll((compiledTypes as Iterable<DartClass>).cast<DartDeclaration>())
        continue
      }
      if declaration is HirImpl {
        // Impl contents are directly compiled into their target traits or types.
        continue
      }
      if declaration is HirType {
        let compiledTypes = compileType(context, package, declaration as HirType)
        declarations.appendAll((compiledTypes as Iterable<DartClass>).cast<DartDeclaration>())
        continue
      }
      if declaration is HirFunction {
        declarations.append(compileGlobalFunction(context, declaration as HirFunction))
        continue
      }
      assert(false, "Unknown declaration type: {declaration}.")
    }

    let compilationUnit = DartCompilationUnit(
      directives = List.empty<DartDirective>(),
      declarations = declarations,
    )
    let importAllocator = DartSimplePrefixingImportAllocator(MutableMap.empty<String, String>())
    let code = (compilationUnit as DartToCode).toCode(importAllocator)

    Tuple(Tuple(outputPath, code), List.empty<CompilerError>())
  })
}

public fun moduleHirToOutputPath(context: QueryContext<List<CompilerError>>, moduleHir: HirModule): Path {
  query<Path, List<CompilerError>>(context, "code_generator_dart.moduleHirToOutputPath", moduleHir, {
    let projectName = mangleName(context, (moduleHir as HasPackageAndPath).package().getName(context))
    let path = ((moduleHir as HasPackageAndPath).path() as Iterable<String>)
        .map<String>({ mangleName(context, it) })
        .join("/")
    let path = Path.parse("{path}.{dartFileExtension}")
    Tuple(dartSourcePath.child(projectName).append(path), List.empty<CompilerError>())
  })
}
public fun moduleHirToImportUrl(context: QueryContext<List<CompilerError>>, moduleHir: HirModule): String {
  query<String, List<CompilerError>>(context, "code_generator_dart.moduleHirToImportUrl", moduleHir, {
    let projectName = mangleName(context, (moduleHir as HasPackageAndPath).package().getName(context))
    let path = ((moduleHir as HasPackageAndPath).path() as Iterable<String>)
        .map<String>({ mangleName(context, it) })
        .join("/")
    Tuple(
      "package:{outputPackageName}/{projectName}/{path}.{dartFileExtension}",
      List.empty<CompilerError>(),
    )
  })
}
