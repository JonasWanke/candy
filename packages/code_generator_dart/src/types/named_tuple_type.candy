use compiler_utils
use dart_code
use hir
use incremental

use ...declarations
use ...types
use ..type_collection

public fun compileNamedTupleType(
  context: QueryContext<List<CompilerError>>,
  namedTupleType: HirNamedTupleType,
): DartType {
  query<DartType, List<CompilerError>>(
    context,
    "code_generator_dart.compileNamedTupleType",
    namedTupleType, {
    let sortedFields = (namedTupleType.types.sortedBy<String>({ it.first }) as Iterable<(String, HirInlineType)>)
    let fieldNames = sortedFields.map<String>({ it.first }).toSet()
    let name = getCompiledNamedTupleTypeName(context, fieldNames)
    let importUrl = moduleHirToImportUrl(context, HirModule.syntheticNamedTuples(context))
    let name = DartIdentifier(name, Some<String>(importUrl))

    let types = sortedFields.map<DartType>({ compileInlineType(context, it.second) }).toList()

    Tuple(DartNamedType(name, types), List.empty<CompilerError>())
  })
}

public fun compileNamedTupleTypeDeclarations(
  context: QueryContext<List<CompilerError>>,
  package: Package,
): (HirModule, List<DartDeclaration>) {
  query<(HirModule, List<DartDeclaration>), List<CompilerError>>(
    context,
    "code_generator_dart.compileNamedTupleTypeDeclarations",
    package, {
    let fieldNames = getAllInlineTypes(context, package).items()
      .whereType<HirNamedTupleType>()
      .map<Set<String>>({
        (it.types as Iterable<(String, HirInlineType)>).map<String>({ it.first }).toSet()
      })
      .toSet()
    let classes = fieldNames.items()
      .map<DartDeclaration>({ compileNamedTupleTypeDeclaration(context, it) })
      .toList()
    Tuple(Tuple(HirModule.syntheticNamedTuples(context), classes), List.empty<CompilerError>())
  })
}
fun compileNamedTupleTypeDeclaration(
  context: QueryContext<List<CompilerError>>,
  fieldNames: Set<String>,
): DartClass {
  query<DartClass, List<CompilerError>>(
    context,
    "code_generator_dart.compileNamedTupleTypeDeclaration",
    SetOfString(fieldNames), {
    let name = getCompiledNamedTupleTypeName(context, fieldNames)

    let fields = (fieldNames.items().toList().unsafeSorted() as Iterable<String>)
      .map<(String, String)>({ Tuple(it, "T{it}") })

    let body = MutableList.empty<DartConstructor | DartField | DartGetter | DartSetter | DartFunction>()

    let constructor = DartConstructor(
      className = name,
      name = None<String>(),
      docComment = None<String>(),
      annotations = List.empty<DartAnnotation>(),
      isConst = true,
      requiredParameters = List.empty<DartParameter | DartInitializingFormal>(),
      positionalParameters = List.empty<DartParameter | DartInitializingFormal>(),
      namedParameters = fields
        .map<DartParameter | DartInitializingFormal>({
          DartInitializingFormal(it.first, defaultValue = None<DartExpression>())
        })
        .toList(),
      body = None<DartBody>(),
    )
    body.append(constructor)

    let dartFields = fields
      .map<DartConstructor | DartField | DartGetter | DartSetter | DartFunction>({
        DartField(
          it.first,
          docComment = None<String>(),
          // TODO(JonasWanke): Add the corresponding Candy code as the doc comment.
          isStatic = false,
          mutability = DartFinal(),
          type = Some<DartType>(
            DartNamedType(DartIdentifier(it.second, None<String>()), List.empty<DartType>()),
          ),
          initialValue = None<DartExpression>(),
        )
      })
    body.appendAll(dartFields)

    let dartDeclaration = DartClass(
      name = name,
      docComment = None<String>(),
      // TODO(JonasWanke): Add the corresponding Candy code as the doc comment.
      annotations = List.of1<DartAnnotation>(dartMetaSealedAnnotation),
      isAbstract = false,
      typeParameters = fields
        .map<DartTypeParameter>({ DartTypeParameter(it.second, None<DartType>()) })
        .toList(),
      extends_ = None<DartType>(),
      implements_ = List.empty<DartType>(),
      with_ = List.empty<DartType>(),
      body = body,
    )

    Tuple(dartDeclaration, List.empty<CompilerError>())
  })
}

public fun getCompiledNamedTupleTypeName(
  context: QueryContext<List<CompilerError>>,
  fieldNames: Set<String>,
): String {
  query<String, List<CompilerError>>(
    context,
    "code_generator_dart.getCompiledNamedTupleTypeName",
    SetOfString(fieldNames), {
    let fieldNames = fieldNames.items().toList().unsafeSorted()
    let fieldNames = (fieldNames as Iterable<String>).join("$")
    Tuple("NamedTuple${fieldNames}", List.empty<CompilerError>())
  })
}
