use compiler_utils
use dart_code
use hir
use incremental

use ...declarations
use ...types

public fun getValueExpressionResultIdentifier(
  context: QueryContext<List<CompilerError>>,
  expressionUri: HirValueExpressionUri,
  expectedType: HirInlineType
): DartExpression {
  query<DartExpression, List<CompilerError>>(
    context,
    "code_generator_dart.getValueExpressionResultIdentifier",
    DataTuple2<HirValueExpressionUri, HirInlineType>(expressionUri, expectedType), {
    let name = getValueExpressionResultName(context, expressionUri)
    let identifier = DartIdentifier(name, None<String>())
    
    mut let result = (identifier as DartExpression)
    let actualType = resolveHirValueExpressionUri(context, expressionUri).type(context)
    if (expectedType is HirNamedType && !(expectedType == actualType)) {
      if ((expectedType as HirNamedType).declaration is HirTrait) {
        let mangledTraitName = getTraitNameMangledForMethodName(
          context,
          expectedType as HirNamedType,
        )
        result = identifier.dot("as{mangledTraitName}")
      }
    }

    Tuple(result, List.empty<CompilerError>())
  })
}
public fun getValueExpressionResultName(
  context: QueryContext<List<CompilerError>>,
  expressionUri: HirValueExpressionUri,
): String {
  query<String, List<CompilerError>>(
    context,
    "code_generator_dart.getValueExpressionResultName",
    expressionUri, {
    Tuple("${expressionUri.index}", List.empty<CompilerError>())
  })
}

// public fun saveSingleExpression(
//   context: QueryContext<List<CompilerError>>,
//   hirExpression: HirValueExpression,
//   dartExpression: DartExpression,
// ): List<DartStatement> {
//   query<List<DartStatement>, List<CompilerError>>(
//     context,
//     "code_generator_dart.saveSingleExpression",
//     DataTuple2<HirValueExpression, DartExpression>(hirExpression, dartExpression), {
//     let statement = saveValueExpression(context, hirExpression, dartExpression)
//     Tuple(List.of1<DartStatement>(statement), List.empty<CompilerError>())
//   })
// }
public fun saveNothing(
  context: QueryContext<List<CompilerError>>,
  uri: HirValueExpressionUri,
): DartStatement {
  query<DartStatement, List<CompilerError>>(
    context,
    "code_generator_dart.saveNothing",
    uri, {
    let nothingType = compileInlineType(context, HirNamedType.coreNothing(context))
    let field = DartField(
      name = getValueExpressionResultName(context, uri),
      docComment = None<String>(),
      isStatic = false,
      mutability = DartFinal(),
      type = Some<DartType>(nothingType),
      initialValue = Some<DartExpression>((nothingType as DartExpression).call0()),
    )
    Tuple(DartFieldStatement(field), List.empty<CompilerError>())
  })
}

public fun saveValueExpression(
  context: QueryContext<List<CompilerError>>,
  uri: HirValueExpressionUri,
  hirExpression: HirValueExpression,
  dartExpression: DartExpression,
): DartStatement {
  query<DartStatement, List<CompilerError>>(
    context,
    "code_generator_dart.saveValueExpression",
    DataTuple3<HirValueExpressionUri, HirValueExpression, DartExpression>(uri, hirExpression, dartExpression), {
    let field = DartField(
      name = getValueExpressionResultName(context, uri),
      docComment = None<String>(),
      isStatic = false,
      mutability = DartFinal(),
      type = Some<DartType>(compileInlineType(context, hirExpression.type(context))),
      initialValue = Some<DartExpression>(dartExpression),
    )
    Tuple(DartFieldStatement(field), List.empty<CompilerError>())
  })
}
