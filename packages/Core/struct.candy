builtins = use "Builtins"
[ifElse] = use "..controlFlow"
type = use "..type"

is value := type.is value Struct

hasKey struct key :=
  needs (is struct)
  struct | builtins.structHasKey key

get struct key :=
  needs (is struct)
  ifElse (struct | hasKey key) { Ok (struct | builtins.structGet key) } { Error KeyNotInStruct }

getKeys struct :=
  needs (is struct)
  struct | builtins.structGetKeys

test =
  [checkEquals] = use "..check"

  checkEquals (is 3) False
  checkEquals (is []) True

  checkEquals (hasKey [Foo: 2] Foo) True
  checkEquals (hasKey [Foo: 2] Bar) False

  checkEquals (get [Foo: 2] Foo) (Ok 2)
  checkEquals (get [Foo: 2] Bar) (Error KeyNotInStruct)

  checkEquals (getKeys [Foo: 2]) (Foo,)
  ## For bigger structs, the order of keys is not guaranteed.
