bool = use "..bool"
equals = (use "..equality").equals
if = (use "..conditionals").if
isInt = (use "..int").is
isType = (use "..type").is

isSendPort value := isType value SendPort
isReceivePort value := isType value ReceivePort

create capacity :=
  needs (isInt capacity) "`capacity` is not an integer. Channels needs a capacity because otherwise, there would be no backpressure among fibers and memory leaks would go unnoticed."
  ✨.channelCreate capacity

send port packet :=
  needs (bool.not (isReceivePort port)) "`port` should be a send port, not a receive port."
  needs (isSendPort port)
  ✨.channelSend port packet

receive port :=
  needs (bool.not (isSendPort port)) "`port` should be a receive port, not a send port"
  needs (isReceivePort port)
  ✨.channelReceive port
